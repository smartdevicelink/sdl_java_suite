apply plugin: 'com.android.library'

android {
    compileSdkVersion 34
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 34
        versionCode 26
        versionName new File(projectDir.path, ('/../../VERSION')).text.trim()
        buildConfigField "String", "VERSION_NAME", '\"' + versionName + '\"'
        resValue "string", "SDL_LIB_VERSION", '\"' + versionName + '\"'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
        }
        debug {
            testCoverageEnabled = true
            multiDexEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += '../../base/src/main/java'
        androidTest.assets.srcDirs += '../../generator/rpc_spec/'
    }

    buildFeatures {
        aidl true
    }

    namespace 'com.smartdevicelink'
    testNamespace 'com.smartdevicelink.test'
}

ext { VERSION_NAME = "$project.android.defaultConfig.versionName" }

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    //api 'com.livio.taskmaster:taskmaster:0.6.0'
    api 'com.smartdevicelink:bson_java_port:1.2.5'
    api 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    api 'androidx.annotation:annotation:1.1.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    androidTestImplementation 'org.mockito:mockito-core:5.7.0'
    androidTestImplementation 'org.mockito:mockito-android:5.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.27.0'
    }
}

apply plugin: "com.vanniktech.maven.publish"
